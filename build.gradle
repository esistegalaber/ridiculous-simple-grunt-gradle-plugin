/**
 * Call-Through for "npm install"
 */
task nodeLoad(type: Exec, dependsOn: 'nodeClean') {
    description 'Call-Through for "npm install"'
    workingDir project.projectDir
    commandLine 'npm', 'install'
    doLast {
        logger.quiet("Downloaded NPM Modules")
    }
}

/**
 * Clean "node_modules" directory
 */
task nodeClean(type: Delete) {
    description 'Clean "node_modules" directory'
    delete 'node_modules'
    doLast {
        logger.quiet("Cleaned NPM Modules")
    }
}

/**
 * Call-Through for "bower install"
 */
task bowerLoad(type: Exec, dependsOn: 'bowerClean') {
    description 'Call-Through for "bower install"'
    workingDir project.projectDir
    commandLine 'bower', 'install'
    doLast {
        logger.quiet("Downloaded Bower Components")
    }
}

/**
 * Clean "bower_components" directory
 */
task bowerClean(type: Delete) {
    description 'Clean "bower_components" directory'
    delete 'bower_components'
    doLast {
        logger.quiet("Cleaned Bower Components")
    }
}

/**
 * Set up the JavaScript dependencies via NPM and Bower.
 */
task setup(dependsOn: ['nodeLoad', 'bowerLoad']) {
    description 'Set up the JavaScript dependencies via NPM and Bower.'
    doLast {
        logger.quiet("Setup complete")
    }
}

/**
 * Compile the Coffee Sources to JS during 'compileJava'.
 */
task compileCoffee(type: Exec) {
    workingDir project.projectDir
    commandLine 'grunt', 'compile'
}
compileJava.dependsOn('compileCoffee')

/**
 * Compile the Coffee Test Sources and execute Jasmine tests during 'test'.
 */
task testJs(type: Exec, dependsOn: 'compileCoffee') {
    workingDir project.projectDir
    commandLine 'grunt', 'test'
}
test.dependsOn('testJs')

/**
 * Clean the generated / copied js and styling resources.
 */
task cleanFrontend(type: Delete) {
    delete 'src/main/webapp/js', 'src/main/webapp/styles', 'src/main/webapp/fonts'
}
clean.dependsOn 'cleanFrontend'